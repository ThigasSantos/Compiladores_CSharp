INICIO -> PROGRAM
PROGRAM -> USING_LIST CLASS_DECL
USING_LIST -> USING_DECL USING_LIST
USING_LIST ->
USING_DECL -> USING NAMESPACE SEMI
NAMESPACE -> ID NAMESPACE_DOT
NAMESPACE_DOT -> DOT ID
NAMESPACE_DOT ->
CLASS_DECL -> CLASS ID LBRACE MEMBER_DECLS RBRACE
MEMBER_DECLS -> MEMBER_DECL MEMBER_DECLS
MEMBER_DECLS ->
MEMBER_DECL -> FIELD_DECL
MEMBER_DECL -> METHOD_DECL
FIELD_DECL -> TYPE ID SEMI
FIELD_DECL -> TYPE ID ASSIGN EXPRESSION SEMI
METHOD_DECL -> METHOD_ACCESS BLOCK
METHOD_ACCESS -> PUBLIC METHOD_MODIFIER
METHOD_ACCESS -> PRIVATE METHOD_MODIFIER
METHOD_ACCESS -> METHOD_MODIFIER
METHOD_MODIFIER -> STATIC METHOD_HEADER
METHOD_MODIFIER -> METHOD_HEADER
METHOD_HEADER -> TYPE ID LPAREN PARAM_LIST RPAREN
PARAM_LIST -> PARAM_LIST COMMA PARAM
PARAM_LIST -> PARAM
PARAM_LIST ->
PARAM -> TYPE ID
BLOCK -> LBRACE STATEMENTS RBRACE
STATEMENTS -> STATEMENT STATEMENTS
STATEMENTS ->
STATEMENT -> IF LPAREN EXPRESSION RPAREN STATEMENT ELSE_STATEMENT
STATEMENT -> BLOCK
STATEMENT -> WHILE_RULE STATEMENT
STATEMENT -> RETURN_RULE SEMI
STATEMENT -> FIELD_DECL
STATEMENT -> VARIABLE_ASSIGN SEMI
STATEMENT -> EXPRESSION SEMI
ELSE_STATEMENT -> ELSE STATEMENT
ELSE_STATEMENT ->
WHILE_RULE -> WHILE LPAREN EXPRESSION RPAREN STATEMENT
RETURN_RULE -> RETURN EXPRESSION_OPT
EXPRESSION_OPT -> EXPRESSION
EXPRESSION_OPT ->
VARIABLE_ASSIGN -> ID_RULE ASSIGN EXPRESSION
EXPRESSION -> LOGICAL_OR
LOGICAL_OR -> LOGICAL_OR OR LOGICAL_AND
LOGICAL_OR -> LOGICAL_AND
LOGICAL_AND -> LOGICAL_AND AND EQUALITY
LOGICAL_AND -> EQUALITY
EQUALITY -> RELATIONAL EQ RELATIONAL
EQUALITY -> RELATIONAL NEQ RELATIONAL
EQUALITY -> RELATIONAL
RELATIONAL -> RELATIONAL LT ADDITIVE
RELATIONAL -> RELATIONAL LTE ADDITIVE
RELATIONAL -> RELATIONAL GT ADDITIVE
RELATIONAL -> RELATIONAL GTE ADDITIVE
RELATIONAL -> ADDITIVE
ADDITIVE -> ADDITIVE PLUS MULTIPLICATIVE
ADDITIVE -> ADDITIVE MINUS MULTIPLICATIVE
ADDITIVE -> MULTIPLICATIVE
MULTIPLICATIVE -> MULTIPLICATIVE MULT UNARY
MULTIPLICATIVE -> MULTIPLICATIVE DIV UNARY
MULTIPLICATIVE -> MULTIPLICATIVE MOD UNARY
MULTIPLICATIVE -> UNARY
UNARY -> NOT PRIMARY
UNARY -> PLUS PRIMARY
UNARY -> MINUS PRIMARY
UNARY -> PRIMARY
ID_RULE -> ID MEMBER_ACCESS
MEMBER_ACCESS -> DOT ID MEMBER_ACCESS
MEMBER_ACCESS -> LBRACKET EXPRESSION RBRACKET MEMBER_ACCESS
MEMBER_ACCESS ->
PRIMARY -> LITERAL_INTEGER
PRIMARY -> LITERAL_STRING
PRIMARY -> CHAR_LITERAL
PRIMARY -> INTERPOLATED_LITERAL_STRING
PRIMARY -> TRUE 
PRIMARY -> FALSE
PRIMARY -> REAL_LITERAL
PRIMARY -> ID_RULE
PRIMARY -> ID_RULE LPAREN ARG_LIST RPAREN
PRIMARY -> LPAREN EXPRESSION RPAREN
PRIMARY -> ARRAY_CREATION
ARRAY_CREATION -> NEW TYPE
ARRAY_CREATION -> NEW TYPE ARRAY_INITIALIZER
ARRAY_CREATION -> NEW SIMPLE_TYPE LBRACKET EXPRESSION RBRACKET
ARRAY_CREATION -> NEW SIMPLE_TYPE LBRACKET EXPRESSION RBRACKET ARRAY_INITIALIZER
ARRAY_INITIALIZER -> LBRACE ELEMENT_LIST RBRACE
ELEMENT_LIST -> ELEMENT
ELEMENT_LIST -> ELEMENT_LIST COMMA ELEMENT
ELEMENT -> EXPRESSION
ELEMENT -> ARRAY_CREATION
ARG_LIST -> ARG_LIST COMMA EXPRESSION
ARG_LIST -> EXPRESSION
ARG_LIST ->
TYPE -> SIMPLE_TYPE TYPE_DIMENSION
TYPE_DIMENSION -> LBRACKET RBRACKET TYPE_DIMENSION
TYPE_DIMENSION ->
SIMPLE_TYPE -> INT
SIMPLE_TYPE -> FLOAT
SIMPLE_TYPE -> DOUBLE
SIMPLE_TYPE -> CHAR
SIMPLE_TYPE -> STRING
SIMPLE_TYPE -> BOOL
SIMPLE_TYPE -> VOID
